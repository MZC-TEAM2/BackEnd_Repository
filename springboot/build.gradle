buildscript {
    dependencies {
        classpath 'com.mysql:mysql-connector-j:9.4.0'
        classpath 'org.flywaydb:flyway-mysql:11.7.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '6.0.26'
    id 'org.flywaydb.flyway' version '11.7.2'
}

group = 'com.mzc.backend'
version = '1'
description = 'mzc 1st Project LMS backend Server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring Boot Staters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'




    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'
    
    // Flyway용 MySQL 드라이버 추가
    implementation 'com.mysql:mysql-connector-j'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Dotenv for loading .env files
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Distributed Lock - ShedLock (for Scheduler)
    implementation 'net.javacrumbs.shedlock:shedlock-spring:6.0.2'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-redis-spring:6.0.2'

    // Distributed Lock - Redisson (for Business Logic)
    implementation 'org.redisson:redisson-spring-boot-starter:3.40.2'

    // Image Processing (WebP)
    implementation 'com.sksamuel.scrimage:scrimage-core:4.1.3'
    implementation 'com.sksamuel.scrimage:scrimage-webp:4.1.3'

    // ULID Generator
    implementation 'com.github.f4b6a3:ulid-creator:5.2.3'

    // Swagger (SpringDoc OpenAPI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    //TEST
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    // Test - H2 for integration tests
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // Test - Slf4j
	testImplementation 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

}

tasks.named('compileTestJava') {
    // Notification 테스트 컴파일 제외
    exclude '**/notification/**'
}

tasks.named('test') {
    useJUnitPlatform()
    // Notification 테스트 제외
    exclude '**/notification/**'
}



// CheckStyle Configuration
checkstyle {
    toolVersion = '10.21.1'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configDirectory = file("${rootDir}/config/checkstyle")
    ignoreFailures = true
    maxWarnings = Integer.MAX_VALUE
    showViolations = true
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

// PMD Configuration
pmd {
    toolVersion = '7.0.0'
    consoleOutput = true
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("${rootDir}/config/pmd/pmd-ruleset.xml")
}

pmdMain {
    source = 'src/main/java'
}

pmdTest {
    source = 'src/test/java'
}

// SpotBugs Configuration
spotbugs {
    toolVersion = '4.8.3'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'medium'
    excludeFilter = file("${rootDir}/config/spotbugs/excludeFilter.xml")
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("${buildDir}/reports/spotbugs/main/spotbugs.html")
        }
        xml {
            required = false
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("${buildDir}/reports/spotbugs/test/spotbugs.html")
        }
        xml {
            required = false
        }
    }
}

// Code Quality Task
task codeQuality {
    group = 'verification'
    description = 'Run all code quality checks (CheckStyle, PMD, SpotBugs)'
    dependsOn checkstyleMain, checkstyleTest, pmdMain, pmdTest, spotbugsMain, spotbugsTest
}

// Build task depends on code quality
tasks.named('build') {
    dependsOn codeQuality
}

// Flyway configuration
flyway {
    url = 'jdbc:mysql://localhost:3306/lms_db?useSSL=false&allowPublicKeyRetrieval=true&serverTimezone=Asia/Seoul'
    user = 'root'
    password = 'rootpassword'
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
}
